[[
    #Inicio
    PAGEPAUSE 1
    BORDER 0
    BRIGHT 1
    FLASH 0
    PAPER 0
    CLEAR
    INK 6
]]
[[REPCHAR 42,42]]
*[[TAB 40]]*
*[[TAB 10 : INK 5]]CRONICAS DE ARKALAND[[TAB 10: INK 6]]*
*[[TAB 40]]*
*[[TAB 11 : INK 7]]de Javier San José[[TAB 11: INK 6]]*
*[[TAB 40]]*
[[REPCHAR 42,42]]
[[INK 7]]
[[TAB 3]]Publicado en CAAD, del num. 1 al 10.



[[TAB 6]]Transcripción por [[INK 2]]Tranqui69[[INK 7]]

[[TAB 8]]Adaptación por [[INK 4]]Cronomantic[[INK 7]]

[[TAB 5]]Hecho con [[INK 5]]ChooseYourDestiny v0.6[[INK 7]]



[[TAB 3]]Pulsa [[INK 6]]ENTER [[INK 7]]o [[INK 6]]SPACE [[INK 7]]para continuar.
[[  
    CENTER
    WAITKEY
    CLEAR
    TAB 12
    INK 5]]INSTRUCCIONES[[INK 7]]

- Pulsa [[INK 6]]ENTER [[INK 7]]o [[INK 6]]SPACE [[INK 7]]para avanzar el texto o confirmar una selección.

- Pulsa [[INK 6]]Q [[INK 7]]o [[INK 6]]A [[INK 7]]para seleccionar una opción cuando se presente una decisión.

[[TAB 18]]***

Ahora se detallará el sistema de juego. ¿Deseas leerlo antes?

[[TAB 4: OPTION GOTO LeerManual :INK 4]]Sí.[[INK 7]]

[[TAB 4: OPTION GOTO SaltarManual :INK 2]]No.[[INK 7]]

(Es recomendable leérselo si juegas por primera vez)
[[CHOOSE]]
[[  #LeerManual
    #SaltarManual
    CENTER : WAITKEY]]

[[/*--------------------------------------*/]]
[[
    /* Cabecera genérica*/
    PAGEPAUSE 1
    BORDER 0
    BRIGHT 1
    FLASH 0
    PAPER 0
    CLEAR
    INK 7
]][[
    /* Declaración de variables */
    DECLARE 255 AS tmp1
    DECLARE 254 AS tmp2
    DECLARE 253 AS tmp3
    DECLARE 252 AS tmp4

    DECLARE 0 AS PuntosVitales
    DECLARE 7 AS Destreza
    DECLARE 8 AS Suerte

    DECLARE 1 AS ProteccionCabeza
    DECLARE 2 AS ProteccionBrazoIzq
    DECLARE 3 AS ProteccionBrazoDer
    DECLARE 4 AS ProteccionPiernaIzq
    DECLARE 5 AS ProteccionPiernaDer
    DECLARE 6 AS ProteccionCuerpo

    DECLARE 9 AS NumRacionesComida
    DECLARE 10 AS NumDoblones

    #GeneracionPersonaje
    RANDOMIZE
    #Reroll
    CLEAR

    SET PuntosVitales TO RANDOM(1 + 18, 12 + 18)
    SET Destreza TO RANDOM(1,12)
    SET Suerte TO RANDOM(1 + 3, 12 + 3)

    SET ProteccionCabeza TO RANDOM(1 + 11, 6 + 11)
    SET ProteccionBrazoIzq TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionBrazoDer TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionPiernaIzq TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionPiernaDer TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionCuerpo TO RANDOM(1 + 12, 6 + 12)

    SET NumRacionesComida TO 4
    SET NumDoblones TO RANDOM(1 + 18, 12 + 18)

    TAB 7]]Estadísticas:

- Puntos Vitales: [[PRINT @PuntosVitales]]
- Destreza:       [[PRINT @Destreza]]
- Suerte:         [[PRINT @Suerte]]

- Protección Cabeza:     [[PRINT @ProteccionCabeza]]
- Protección Brazo Izq.: [[PRINT @ProteccionBrazoIzq]]
- Protección Cuerpo:     [[PRINT @ProteccionCuerpo]]
- Protección Brazo Der.: [[PRINT @ProteccionBrazoDer]]
- Protección Brazo Izq.: [[PRINT @ProteccionPiernaIzq]]
- Protección Brazo Der.: [[PRINT @ProteccionPiernaDer]]

- Doblones:           [[PRINT @NumDoblones]]
- Raciones de comida: [[PRINT @NumRacionesComida]]

¿Estás satisfecho con estos valores?

[[TAB 4: OPTION GOTO GeneracionPersonaje : INK 4]]Sí.[[INK 7]]

[[TAB 4: OPTION GOTO Reroll : INK 2]]No.[[INK 7]]
[[CHOOSE]][[CENTER : WAITKEY : GOTO GeneracionPersonaje]]


[[/*--------------------------------------*/]]

[[
    /* Cabecera genérica*/
    PAGEPAUSE 1
    BORDER 0
    BRIGHT 1
    FLASH 0
    PAPER 0
    CLEAR
    INK 7
]][[
    
    DECLARE 11 AS rejillaCabeza
    DECLARE 12 AS rejillaBrazoIzq
    DECLARE 13 AS rejillaBrazoDer
    DECLARE 14 AS rejillaPiernaIzq
    DECLARE 15 AS rejillaPiernaDer
    DECLARE 16 AS rejillaCuerpo

    DECLARE 17 AS filaRejilla
    DECLARE 18 AS columnaRejilla
    DECLARE 19 AS valorCasillaRejilla

    SET columnaRejilla TO 10
    SET filaRejilla TO 4

    GOSUB imprimeRejilla

]][[CENTER : WAITKEY : END

    /* Subrutina imprimeRejilla */
    #imprimeRejilla
    AT @columnaRejilla, @filaRejilla
    CHAR 32 : CHAR 32 : CHAR 32 : CHAR 148 : CHAR 145 : CHAR 145 : CHAR 149 : CHAR 32 : CHAR 32 : CHAR 32
    AT @columnaRejilla, @filaRejilla + 1
    CHAR 32 : CHAR 32 : CHAR 32 : CHAR 144 : SET valorCasillaRejilla TO @rejillaCabeza : GOSUB imprimeValorCasillaRejilla : CHAR 144 : CHAR 32 : CHAR 32 : CHAR 32
    AT @columnaRejilla, @filaRejilla + 2
    CHAR 148 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 149
    AT @columnaRejilla, @filaRejilla + 3
    CHAR 144 : SET valorCasillaRejilla TO @rejillaBrazoIzq : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : SET valorCasillaRejilla TO @rejillaCabeza : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : SET valorCasillaRejilla TO @rejillaBrazoDer : GOSUB imprimeValorCasillaRejilla : CHAR 144
    AT @columnaRejilla, @filaRejilla + 4
    CHAR 150 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 151
    AT @columnaRejilla, @filaRejilla + 5
    CHAR 144 : SET valorCasillaRejilla TO @rejillaPiernaIzq : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : CHAR 32 : CHAR 32 : CHAR 144
    SET valorCasillaRejilla TO @rejillaPiernaDer : GOSUB imprimeValorCasillaRejilla : CHAR 144
    AT @columnaRejilla, @filaRejilla + 6
    CHAR 146 : CHAR 145 : CHAR 145 : CHAR 147 : CHAR 32 : CHAR 32 : CHAR 146 : CHAR 145 : CHAR 145 : CHAR 147
    RETURN
    /**********************************/

    /* Subrutina imprimeValorCasillaRejilla */
    #imprimeValorCasillaRejilla
    IF @valorCasillaRejilla < 100 THEN GOTO imprimeValorCasillaRejillaDecenas
    PRINT 99 : RETURN
    #imprimeValorCasillaRejillaDecenas
    IF @valorCasillaRejilla < 10 THEN GOTO imprimeValorCasillaRejillaUnidades
    PRINT @valorCasillaRejilla : RETURN
    #imprimeValorCasillaRejillaUnidades
    CHAR 32 : PRINT @valorCasillaRejilla : RETURN
    /**********************************/
]]
