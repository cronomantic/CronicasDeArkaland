[[
    /* Variables */
    DECLARE 0 AS PuntosVitales
    DECLARE 7 AS Destreza
    DECLARE 8 AS Suerte

    DECLARE 1 AS ProteccionCabeza
    DECLARE 2 AS ProteccionBrazoIzq
    DECLARE 3 AS ProteccionBrazoDer
    DECLARE 4 AS ProteccionPiernaIzq
    DECLARE 5 AS ProteccionPiernaDer
    DECLARE 6 AS ProteccionCuerpo

    DECLARE 9 AS NumRacionesComida
    DECLARE 10 AS NumDoblones

    DECLARE 11 AS rejillaCabeza
    DECLARE 12 AS rejillaBrazoIzq
    DECLARE 13 AS rejillaBrazoDer
    DECLARE 14 AS rejillaPiernaIzq
    DECLARE 15 AS rejillaPiernaDer
    DECLARE 16 AS rejillaCuerpo

    DECLARE 17 AS filaRejilla
    DECLARE 18 AS columnaRejilla
    DECLARE 19 AS valorCasillaRejilla

    #Inicio
    /* Cabecera genérica*/
    PAGEPAUSE 1
    BORDER 0
    BRIGHT 1
    FLASH 0
    PAPER 0
    CLEAR
    INK 7
]][[
    #GeneracionPersonaje
    RANDOMIZE
    #Reroll
    CLEAR

    SET PuntosVitales TO RANDOM(1 + 18, 12 + 18)
    SET Destreza TO RANDOM(1,12)
    SET Suerte TO RANDOM(1 + 3, 12 + 3)

    SET ProteccionCabeza TO RANDOM(1 + 11, 6 + 11)
    SET ProteccionBrazoIzq TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionBrazoDer TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionPiernaIzq TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionPiernaDer TO RANDOM(1 + 10, 6 + 10)
    SET ProteccionCuerpo TO RANDOM(1 + 12, 6 + 12)

    SET NumRacionesComida TO 4
    SET NumDoblones TO RANDOM(1 + 18, 12 + 18)

    GOSUB imprimeEstadisticasJugador

    INK 7
    AT 0, 12]]¿Estás satisfecho con estos valores?

[[TAB 4 : OPTION GOTO GeneracionPersonaje : INK 4]]Sí.[[INK 7]]

[[TAB 4 : OPTION GOTO Reroll : INK 2]]No.[[
    INK 7 : CHOOSE
]][[CENTER : WAITKEY : GOTO GeneracionPersonaje]]

[[
    /* Subrutina imprimeEstadisticasJugador */
    #imprimeEstadisticasJugador
    SET rejillaCabeza TO @ProteccionCabeza
    SET rejillaBrazoIzq TO @ProteccionBrazoIzq
    SET rejillaBrazoDer TO @ProteccionBrazoDer
    SET rejillaPiernaIzq TO @ProteccionPiernaIzq
    SET rejillaPiernaDer TO @ProteccionPiernaDer
    SET rejillaCuerpo TO @ProteccionCuerpo
    SET columnaRejilla TO 21
    SET filaRejilla TO 4

    AT 12, 0 : INK 6]]ESTADISTICAS[[
    AT 12, 1 : REPCHAR 145, 12]]

[[INK 5]]- Puntos Vitales: [[INK 7: PRINT @PuntosVitales]]
[[INK 5]]- Destreza:[[TAB 7 : INK 7 : PRINT @Destreza]]
[[INK 5]]- Suerte:[[TAB 9 : INK 7: PRINT @Suerte]]


[[INK 6]]- Doblones:[[TAB 11 : INK 7: PRINT @NumDoblones]]
[[INK 4]]- Raciones de comida: [[INK 7: PRINT @NumRacionesComida]]
[[INK 5: AT 20, 3 ]]- Protección:[[
    INK 7 
    GOSUB imprimeRejilla
    RETURN

    /****************************************/

    /* Subrutina imprimeRejilla */
    #imprimeRejilla
    AT @columnaRejilla, @filaRejilla
    CHAR 32 : CHAR 32 : CHAR 32 : CHAR 148 : CHAR 145 : CHAR 145 : CHAR 149 : CHAR 32 : CHAR 32 : CHAR 32
    AT @columnaRejilla, @filaRejilla + 1
    CHAR 32 : CHAR 32 : CHAR 32 : CHAR 144 : SET valorCasillaRejilla TO @rejillaCabeza : GOSUB imprimeValorCasillaRejilla : CHAR 144 : CHAR 32 : CHAR 32 : CHAR 32
    AT @columnaRejilla, @filaRejilla + 2
    CHAR 148 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 149
    AT @columnaRejilla, @filaRejilla + 3
    CHAR 144 : SET valorCasillaRejilla TO @rejillaBrazoIzq : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : SET valorCasillaRejilla TO @rejillaCabeza : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : SET valorCasillaRejilla TO @rejillaBrazoDer : GOSUB imprimeValorCasillaRejilla : CHAR 144
    AT @columnaRejilla, @filaRejilla + 4
    CHAR 150 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 154 : CHAR 145 : CHAR 145 : CHAR 151
    AT @columnaRejilla, @filaRejilla + 5
    CHAR 144 : SET valorCasillaRejilla TO @rejillaPiernaIzq : GOSUB imprimeValorCasillaRejilla
    CHAR 144 : CHAR 32 : CHAR 32 : CHAR 144
    SET valorCasillaRejilla TO @rejillaPiernaDer : GOSUB imprimeValorCasillaRejilla : CHAR 144
    AT @columnaRejilla, @filaRejilla + 6
    CHAR 146 : CHAR 145 : CHAR 145 : CHAR 147 : CHAR 32 : CHAR 32 : CHAR 146 : CHAR 145 : CHAR 145 : CHAR 147
    RETURN
    /**********************************/

    /* Subrutina imprimeValorCasillaRejilla */
    #imprimeValorCasillaRejilla
    IF @valorCasillaRejilla < 100 THEN GOTO imprimeValorCasillaRejillaDecenas
    PRINT 99 : RETURN
    #imprimeValorCasillaRejillaDecenas
    IF @valorCasillaRejilla < 10 THEN GOTO imprimeValorCasillaRejillaUnidades
    PRINT @valorCasillaRejilla : RETURN
    #imprimeValorCasillaRejillaUnidades
    CHAR 32 : PRINT @valorCasillaRejilla : RETURN
    /**********************************/
]]